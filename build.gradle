plugins {
	id 'org.jetbrains.kotlin.jvm' version "$version_kotlin"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$version_kotlin"
    id 'org.jreleaser' version '1.18.0'
    id 'org.openapi.generator' version '7.13.0'
    id 'de.undercouch.download' version "5.6.0"
    id 'java-library'
    id 'idea'
}

repositories {
    mavenCentral()
}

/* Group name of our artifacts */
group = 'org.vitrivr'

/* Current version of our artifacts. */
version = '0.1.11'

/* Define required variables. */
def fullOAS = 'http://localhost:7070/openapi.json'
def oasFile = "${project.projectDir}/vitrivr-engine-server/doc/oas.json"

openApiGenerate {
    generateApiTests = false
    generateModelTests = false
    validateSpec = false
    skipValidateSpec = true

    generatorName = 'kotlin'
    inputSpec = oasFile
    outputDir = file("${project.projectDir}/openapi/doc/kotlin").toString()
    configOptions = [
        npmName: '@vitrivr-engine/api',
        enumPropertyNaming: 'original'
    ]
}

/**
 * Task to generate OAS. Requires backend to run on default port.
 */
tasks.register('generateOAS', Download) {
    /* This requires a locally running backend */
    src fullOAS
    dest oasFile
}