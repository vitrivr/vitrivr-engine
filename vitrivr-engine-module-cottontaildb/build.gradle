plugins {
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    api project(':vitrivr-engine-core')

    /** Cottontail DB Client. */
    implementation group: 'org.vitrivr', name: 'cottontaildb-client', version: version_cottontaildb

    /** gRPC and Protobuf. */
    implementation group: 'io.grpc', name: 'grpc-all', version: version_grpc
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: version_protobuf

    /** vitrivr engine Core is required for running tests. */
    testImplementation(testFixtures(project(':vitrivr-engine-core')))
}

/* Publication of vitrivr engine query to Maven Central. */
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.vitrivr'
            artifactId = 'vitrivr-engine-plugin-cottontaildb'
            version = System.getenv().getOrDefault("MAVEN_PUBLICATION_VERSION", version.toString())
            from components.java
            pom {
                name = 'vitrivr Engine Cottontail DB Plugin'
                description = 'A plugin that adds connectivity to the Cottontail DB vector database.'
                url = 'https://github.com/vitrivr/vitrivr-engine/'
                licenses {
                    license {
                        name = 'MIT License'
                    }
                }
                developers {
                    developer {
                        id = 'ppanopticon'
                        name = 'Ralph Gasser'
                        email = 'ralph.gasser@unibas.ch'
                    }
                    developer {
                        id = 'lucaro'
                        name = 'Luca Rossetto'
                        email = 'rossetto@ifi.uzh.ch'
                    }
                    developer {
                        id = 'net-cscience-raphael'
                        name = 'Raphael Waltens√ºl'
                        email = 'raphael.waltenspuel@unibas.ch'
                    }
                    developer {
                        id = 'rahelarnold98'
                        name = 'Rahel Arnold'
                        email = 'rahel.arnold@unibas.ch'
                    }
                    developer {
                        id = 'rahelarnold98'
                        name = 'faberf'
                        email = 'fynnfirouz.faber@unibas.ch'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/vitrivr/vitrivr-engine.git'
                    url = 'https://github.com/vitrivr/vitrivr-engine/'
                }
            }
        }
    }
    repositories {
        repositories {
            maven {
                def releasesRepoUrl = 'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                name = "ossrh-staging-api"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}