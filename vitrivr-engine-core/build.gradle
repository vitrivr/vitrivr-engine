plugins {
    id 'org.jetbrains.kotlin.plugin.serialization' version "$version_kotlin"
    id 'maven-publish'
    id 'signing'
}

dependencies {
    /** dependencies for exif metadata extraction. */
    implementation group: 'com.drewnoakes', name: 'metadata-extractor', version: version_metadataextractor
    implementation group: 'io.javalin.community.openapi', name: 'javalin-openapi-plugin', version: version_javalin

    /* Test Fixtures from Cottontail DB core. .*/
    testFixturesImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: version_junit
    testFixturesImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: version_junit


}

/* Publication of vitrivr engine core to Maven Central. */
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.vitrivr'
            artifactId = 'vitrivr-engine-core'
            version = System.getenv().getOrDefault("MAVEN_PUBLICATION_VERSION", version.toString())
            from components.java
            pom {
                name = 'vitrivr Engine Core'
                description = 'Core components of the vitrivr multimedia retrieval engine.'
                url = 'https://github.com/vitrivr/vitrivr-engine/'
                licenses {
                    license {
                        name = 'MIT License'
                    }
                }
                developers {
                    developer {
                        id = 'ppanopticon'
                        name = 'Ralph Gasser'
                        email = 'ralph.gasser@unibas.ch'
                    }
                    developer {
                        id = 'lucaro'
                        name = 'Luca Rossetto'
                        email = 'rossetto@ifi.uzh.ch'
                    }
                    developer {
                        id = 'net-cscience-raphael'
                        name = 'Raphael Waltens√ºl'
                        email = 'raphael.waltenspuel@unibas.ch'
                    }
                    developer {
                        id = 'rahelarnold98'
                        name = 'Rahel Arnold'
                        email = 'rahel.arnold@unibas.ch'
                    }
                    developer {
                        id = 'rahelarnold98'
                        name = 'faberf'
                        email = 'fynnfirouz.faber@unibas.ch'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/vitrivr/vitrivr-engine.git'
                    url = 'https://github.com/vitrivr/vitrivr-engine/'
                }
            }
        }
    }
    repositories {
        repositories {
            maven {
                def releasesRepoUrl = 'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://ossrh-staging-api.central.sonatype.com/content/repositories/snapshots/'
                name = "Portal OSSRH Staging API"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
